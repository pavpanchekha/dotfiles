#+TITLE: Emacs Snippets

* W3M Fixes
** New blank tab

I prefer there to be a key to enter a new URL, blank-slate.

#+BEGIN_SRC emacs-lisp
  (defun w3m-clean-slate-goto-url (url)
    (interactive (list (w3m-input-url nil "" nil nil 'feeling-lucky)))
    (w3m-goto-url url))
#+END_SRC

The symmetric operations should clearly exist for tabs.

#+BEGIN_SRC emacs-lisp
  (defun w3m-clean-slate-goto-url-new-session (url)
    (interactive (list (w3m-input-url nil "" nil nil 'feeling-lucky)))
    (w3m-goto-url-new-session url))
#+END_SRC

** Open in Background

Unfortunately, the open-in-new-tab command switches to the new tab
right away; this is silly.

#+BEGIN_SRC emacs-lisp
  (defun w3m-view-this-url-new-session-background ()
    "Display the page of the link under point in a new session, in
  the background. If the region is active, use the
  `w3m-open-all-links-in-new-session' command instead."
    (interactive)

    (save-window-excursion
      (call-interactively 'w3m-view-this-url-new-session)))
#+END_SRC
** Clamp next buffer

When a tab is closed, W3M should shift to the right tab, not the left tab.

#+BEGIN_SRC emacs-lisp
  (defun clamp (a n)
    (cond
     ((< a 0) 0)
     ((> a n) n)
     (t a)))

  (defun w3m-clamp-next-buffer (arg)
    "Turn ARG pages of emacs-w3m buffers ahead."
    (interactive "p")
    (unless arg (setq arg 1))
    (when (and (/= arg 0) (eq major-mode 'w3m-mode))
      (w3m-history-store-position)
      (let* ((buffers (w3m-list-buffers))
         (len (length buffers)))
        (switch-to-buffer
         (nth (clamp (+ arg (- len (length (memq (current-buffer) buffers))))
           len)
          buffers)))
      (w3m-history-restore-position)
      (run-hooks 'w3m-select-buffer-hook)
      (w3m-select-buffer-update)))

  (add-hook 'w3m-delete-buffer-hook (lambda () (w3m-clamp-next-buffer 1)))

#+END_SRC

* LaTeX Compilation

LaTeX also could use a quick tool to compile and view a source file.
I use the =compile= command for this.

#+BEGIN_SRC emacs-lisp
  (defun latex-compile (file)
    (interactive (list (buffer-file-name)))

    (make-local-variable 'latex-compile-outfile)

    (let ((first-run (not (boundp 'latex-compile-outfile))))
      (when (or first-run (not (get-file-buffer latex-compile-outfile)))
        (setf latex-compile-outfile
              (make-temp-file "emacs-latex-compile-" nil ".pdf"))
        (setf first-run t))

      (save-window-excursion
        (shell-command (concat "run -c " file " -o " latex-compile-outfile)))

      (if first-run
          (find-file-other-window latex-compile-outfile)
        (progn
          (set-buffer (get-file-buffer latex-compile-outfile))
          (doc-view-revert-buffer t t)))))
#+END_SRC

* Mu For Emacs

I'm now reading my mail in =mu4e=, from the =mu-git= Arch Linux package.
This package is a lot like =notmuch= in that it's search-based and
works well with =offlineimap=, but has a much nicer interface and tries
very hard to stay compatible with the upstream IMAP folder.

#+BEGIN_SRC emacs-lisp :tangle nil
(add-to-list 'load-path "/usr/share/emacs/site-lisp/mu4e")
(require 'mu4e)
#+END_SRC

I need to tell Mu which folders store what on my system.  Note that I
only sync "important" messages with GMail to my system, so the
archive folder is the "important" folder.

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf mu4e-maildir "~/mail"
        mu4e-sent-folder "/sent"
        mu4e-drafts-folder "/drafts"
        mu4e-trash-folder "/trash"
        mu4e-refile-folder "/important")
  (setf mu4e-attachment-dir  "/tmp")
#+END_SRC

Mail is synced with =offlineimap=, so I tell Mu to run that to get new
mail.  OfflineIMAP will automatically run the Mu indexer.

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf mu4e-get-mail-command "mu index -m ~/mail --my-address me@pavpanchekha.com")
#+END_SRC

Finally, since I'm using GMail, GMail will automatically deal with
filing sent messages, so I can tell Mu to just delete sent messages.

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf mu4e-sent-messages-behavior 'delete)
#+END_SRC

In all of the =mu4e= modes, I want the Evil extensions off, since they
do naught but get in the way.  But somehow, this doesn't work for
=mu4e-view-mode=, so we use a special dispension for that.

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf evil-emacs-state-modes (list* 'mu4e-headers-mode 'mu4e-main-mode
                                      'mu4e-main-mode 'mu4e-view-mode
                                      evil-emacs-state-modes))
  (add-hook 'mu4e-view-mode-hook 'evil-emacs-state)
#+END_SRC

There're a few graphical things to change about Mu4e:

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf mu4e-use-fancy-chars t)
  (setf mu4e-view-show-images t)
  (setf mu4e-headers-skip-duplicates t)
  (setf mu4e-headers-include-related t)
#+END_SRC

** Sending mail

My mail gets sent through the GMail IMAP server. I used to use msmtp for sending mail, but I’ve now switched to using smtpmail.el, since it’s nice to have an Emacs-native solution.

#+BEGIN_SRC emacs-lisp
(setq send-mail-function 'smtpmail-send-it)
(setq message-send-mail-function 'smtpmail-send-it)
#+END_SRC

We now set up SMTPMail to use GMail.

#+BEGIN_SRC emacs-lisp
(setq smtpmail-smtp-server "smtp.gmail.com")
(setq smtpmail-smtp-service 587)
#+END_SRC

We then set up TLS for GMail’s connection:

#+BEGIN_SRC emacs-lisp
  (setq starttls-use-gnutls t)

  (setq smtpmail-default-smtp-server "smtp.gmail.com"
        smtpmail-smtp-server "smtp.gmail.com"
        smtpmail-smtp-service 587
        smtpmail-smtp-user "me@pavpanchekha.com"
        smtpmail-local-domain "pavpanchekha.com"
        smtpmail-sendto-domain "pavpanchekha.com"
        smtpmail-debug-info t)

  (require 'smtpmail)
#+END_SRC

** Composing messages

When composing replies, there are a few things I change from their defaults.  Firstly, I prefer quotes not to have citations, since I rarely participate in massive, many-person email threads.

#+BEGIN_SRC emacs-lisp
(setf message-citation-line-format "")
#+END_SRC

I also have a standard signature.

#+BEGIN_SRC emacs-lisp
(setf message-signature " - Pavel Panchekha")
#+END_SRC

** Bookmarks

I have a few bookmarks that set up my email flow.

#+BEGIN_SRC emacs-lisp :tangle nil
  (setf mu4e-maildir-shortcuts
    '(("/inbox" . ?i)
      ("flag:unread AND maildir:/important"  . ?u)))
#+END_SRC
* Org Mode

** Agenda View

I use the agenda view a lot, so I customize it a bit.  The agenda
should show two days (including, yes, the ones without events); it
shouldn't show me things I've done; and I won't worry about starting
on a weekend or weekday.

#+BEGIN_SRC emacs-lisp
  (setq org-agenda-ndays 1)
  (setq org-agenda-span 2)
  (setq org-deadline-warning-days 3)

  (setq org-agenda-skip-deadline-if-done t)
  (setq org-agenda-skip-scheduled-if-done t)
  (setq org-agenda-start-on-weekday nil)
#+END_SRC

Since all of my TODO entries are in one file, I don't need the agenda
view to tell me what file a TODO entry comes from.

#+BEGIN_SRC emacs-lisp
  (setq org-agenda-file-regexp "\\`[^.].*\\.\\(txt\\|org\\)\\'")
  (setq org-agenda-files '("~/notes/"))
  (setq org-agenda-prefix-format
        '((agenda . " %i %?-12t% s")
          (timeline . "  % s")
          (todo . " %i %-12:c")
          (tags . " %i %-12:c")
          (search . " %i %-12:c")))
#+END_SRC

Finally, given my use of the TODO list, I would like fewer deadline
reminders and more scheduled reminders

#+BEGIN_SRC emacs-lisp
  (setq org-agenda-move-date-from-past-immediately-to-today t)
  (setq org-agenda-skip-deadline-prewarning-if-scheduled t)
  (setq org-agenda-skip-scheduled-if-deadline-is-shown nil)
#+END_SRC

** Todo Keywords

The keywords here represent a good chunk of possible work-flows.  TODO
to DONE is the standard one, but sometimes I go to WONT or spent time
cycling between TODO and WAIT.  SOMEDAY exists for the projects that
I'd like to do, but that are currently in limbo.

#+BEGIN_SRC emacs-lisp
(setq org-todo-keywords
      '((sequence "TODO(t)" "SOMEDAY(s)" "|" "DONE(d)" "WAIT(w)" "WONT(n)")))
(setq org-use-fast-todo-selection t)
#+END_SRC

** Key bindings

Some key bindings are not bound by Org automatically, so I have to bind
them myself.

#+BEGIN_SRC emacs-lisp
(global-set-key "\C-cl" 'org-store-link)
(global-set-key "\C-ca" 'org-agenda)
(global-set-key "\C-cc" 'org-capture)
#+END_SRC
* Multi-term

Multiterm requires a bit more setup.

#+BEGIN_SRC emacs-lisp
(require 'multi-term)
(setq multi-term-program "/bin/bash")
#+END_SRC

For simplicity, I also add the =mterm= command to just call =multi-term=.

#+BEGIN_SRC emacs-lisp
(defun mterm ()
  (interactive)
  (multi-term))
#+END_SRC

One annoyance is that the screen bounces in multi-term due to my =scroll-margin=.  But there's a solution: file-local variables.  We attach a handler to =term-mode-hook= that makes =scroll-margin= local and then sets it to =0=.  Because =make-local-variable= returns the variable (as a symbol), we can call =set= (that's right: not =setq=, but =set=, since we already have the variable quoted for us) to set it.

#+BEGIN_SRC emacs-lisp
(add-to-list 'term-mode-hook (lambda ()
  (set (make-local-variable 'scroll-margin) 0)))
#+END_SRC
* Defining apps

When I ran everything inside Emacs, I’d like separate windows for different “Emacs apps”. Here’s how I defined them:

#+BEGIN_SRC emacs-lisp
  (require 'cl)

  (defmacro* defapp (keybinding &rest cmd)
    (let ((name (gensym)))
      `(progn
         (defun ,name ()
           (interactive)
           (select-frame (new-frame))
           ,@cmd)
         (global-set-key (kbd ,keybinding) ',name))))
#+END_SRC

Some apps I had defined:

: (defapp "s-X" (eshell))
: (defapp "s-M" (rmail))
: (defapp "s-L" (slime))
: (defapp "s-J" (jabber-switch-to-roster-buffer))
: (defapp "s-A" (org-agenda-list))
: (defapp "s-Z" (w3m))
: (defapp "s-G" (magit-status "~/"))

* Jabber

It’s nice to run chat inside Emacs, but the default configuration is awfully verbose. I’d prefer less textual output:

#+BEGIN_SRC emacs-lisp
(setq jabber-account-list '(("me@pavpanchekha.com" 
                             (:network-server . "talk.google.com") 
                             (:connection-type . ssl)))
      jabber-avatar-cache-directory "/tmp/jabber-avatars"
      jabber-backlog-days 3.0
      jabber-chat-buffer-show-avatar nil
      jabber-chat-foreign-prompt-format "> "
      jabber-chat-local-prompt-format "> "
      jabber-chat-system-prompt-format "*** "
      jabber-chat-time-format "%H:%M"
      jabber-default-show ""
      jabber-groupchat-prompt-format "%n> "
      jabber-muc-private-foreign-prompt-format "%g/%n> "
      jabber-roster-line-format "%c %-25n %u %-8s  %S"
      jabber-roster-show-title nil
      jabber-show-offline-contacts nil)

(custom-set-faces
 '(jabber-chat-prompt-foreign ((t (:foreground "red"))))
 '(jabber-chat-prompt-local ((t (:foreground "blue"))))
 '(jabber-chat-prompt-system ((t (:foreground "dark green" :weight bold))))
 '(jabber-roster-user-away ((t (:foreground "orange"))))
 '(jabber-roster-user-chatty ((t (:foreground "green"))))
 '(jabber-roster-user-online ((t (:foreground "dark green")))))

; Auto-urlize urls
(add-hook 'jabber-chat-mode-hook 'goto-address)
(jabber-connect-all)
#+END_SRC

* Dropbox

[[http://github.com/pavpanchekha/dropbox.el/][dropbox.el]] is a Dropbox client I wrote -- it accesses files from Dropbox through the [[http://developers.dropbox.com][Dropbox API]].  It needs a bit of setup, mostly to set the API secret and key.  The actual values are in the [[file:passwords.org][passwords file]], but I unpack them here to remind myself that the entry in the passwords file exists.

#+BEGIN_SRC emacs-lisp
  (load "~/Dropbox/Work/dropbox.el/dropbox.el")
  (setf dropbox-consumer-key    (car  my-dropbox-key))
  (setf dropbox-consumer-secret (cadr my-dropbox-key))
#+END_SRC

Since I don't have many concurrent clients and rarely use shared folders, a longer cache time makes a lot of sense.

#+BEGIN_SRC emacs-lisp
  (setf dropbox-cache-timeout 300)
#+END_SRC
